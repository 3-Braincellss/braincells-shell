%import common.LETTER

command : pipe | seq | call | WHITESPACE
seq     : command ";" command
pipe    : call "|" call
        | pipe "|" call
call        : WHITESPACE? (redirection WHITESPACE)* arguments (WHITESPACE redirection)* WHITESPACE?
arguments     : word (WHITESPACE word)* 

word    : (quoted | UNQUOTED)
redirection : "<" WHITESPACE word
            | ">" WHITESPACE word

quoted        : single_quoted | double_quoted | back_quoted
double_quoted : "\"" (back_quoted | DOUBLE_QUOTE_CONTENT)* "\""
back_quoted    : "`" BACKQUOTED "`" 
single_quoted : "'" SINGLE_QUOTE_CONTENT* "'"

BACKQUOTED           : /[^\n`]+/
DOUBLE_QUOTE_CONTENT : /[^\n"`]+/
SINGLE_QUOTE_CONTENT : /[^\n']+/
UNQUOTED             : /[^\s"'`\n;|<>]+/
WHITESPACE           : /[\s]+/